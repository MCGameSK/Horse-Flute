#
#Github https://github.com/MCGameSK/Horse-Flute
#



import:
    org.bukkit.inventory.ItemFlag



options:
    FluteTag: 360211
    StableTag: 360212



on load:
    set {flute} to nautilus shell of fortune named "&6&l말 피리" with lore "&r " and "&8&o우클릭으로 말을 설정하고 부를수 있다" and "&r "
    set custom model data of {flute} to {@FluteTag}
    set {_meta} to {flute}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_ENCHANTS])
    {flute}.setItemMeta({_meta})

    set {stable} to leather horse armor of fortune named "&7&l마구간 설정" with lore "&r " and "&8&o우클릭한 위치를 말의 안식처로 결정합니다" and "&r "
    set custom model data of {stable} to {@StableTag}
    set {_meta} to {stable}.getItemMeta()
    {_meta}.addItemFlags([ItemFlag.HIDE_ENCHANTS])
    {stable}.setItemMeta({_meta})



on join:
    delete {calling::%player's uuid%}



on right click on horse with priority normal:
    if 1 of event-item is {flute}:
        cancel event

        if scoreboard tags of clicked entity is set:
            message "&c이미 설정된 말입니다" to player
        else:
            set scoreboard tags of clicked entity to "%player's uuid%"
            message "&a이제 이 말은 당신이 부르면 올 것입니다." to player



on right click with priority high:
    if 1 of event-item is {flute}:
        cancel event

        if {stableLoc::%player's uuid%} is not set:
            message "&c마구간을 설정해야합니다."
            exit

        if {calling::%player's uuid%} is set:
            message "&c말이 오고 있습니다!"
            exit
        set {calling::%player's uuid%} to true

        set {_exist} to false
        set {_horse} to 0
        set {_loc} to player's location
        loop all horses:
            if scoreboard tags of loop-horse is "%player's uuid%":
                set {_exist} to true
                set {_horse} to loop-horse

        if {_exist} is false:
            message "&c부를수 있는 말이 없습니다!" to player
        else:
            play sound "block.note_block.flute" with volume 2 and pitch 1.2 at player
            play sound "block.note_block.guitar" with volume 0.6 and pitch 1.2 at player
            wait for 0.3 seconds
            play sound "block.note_block.flute" with volume 2 and pitch 1.6 at player
            play sound "block.note_block.guitar" with volume 0.6 and pitch 1.6 at player
            wait for 0.15 seconds
            play sound "block.note_block.flute" with volume 2 and pitch 1.5 at player
            play sound "block.note_block.guitar" with volume 0.6 and pitch 1.5 at player
            wait for 0.15 seconds
            play sound "block.note_block.flute" with volume 2 and pitch 1.2 at player
            play sound "block.note_block.guitar" with volume 0.6 and pitch 1.2 at player
            wait for 0.3 seconds
            play sound "block.note_block.flute" with volume 2 and pitch 1 at player
            play sound "block.note_block.guitar" with volume 0.6 and pitch 1 at player
            wait for 0.3 seconds
            play sound "block.note_block.flute" with volume 2 and pitch 1.2 at player
            play sound "block.note_block.guitar" with volume 0.6 and pitch 1.2 at player

            wait for 0.6 seconds

            play sound "entity.generic.explode" with volume 0.4 and pitch 1.2 at {_loc}
            play sound "entity.firework_rocket.launch" with volume 2 and pitch 1.2 at {_loc}

            dismount any passenger from {_horse}
            teleport {_horse} to {_loc}

            show large explosion at {_loc}
            loop 12 times:
                set {_effLoc} to {_loc}
                add random number between -1 and 1 to x-coord of {_effLoc}
                add random number between 0 and 1.5 to y-coord of {_effLoc}
                add random number between -1 and 1 to z-coord of {_effLoc}
                show cloud at {_effLoc}
        
        delete {calling::%player's uuid%}

    else if 1 of event-item is {stable}:
        cancel event

        if event-block is not set:
            message "&c설정할 위치를 보고 우클릭하세요." to player
            exit

        remove {stable} from player's inventory

        if {stableLoc::%player's uuid%} is not set:
            set {stableLoc::%player's uuid%} to event-location
            {stableLoc::%player's uuid%}.getChunk().setForceLoaded(true)
            
            message "&a새로운 마구간이 설정되었습니다." to player
        else:
            {stableLoc::%player's uuid%}.getChunk().setForceLoaded(false)

            set {stableLoc::%player's uuid%} to event-location
            {stableLoc::%player's uuid%}.getChunk().setForceLoaded(true)

            message "&a마구간을 옮겼습니다." to player

        loop all entities:
            if loop-entity is armor stand:
                if loop-entity has scoreboard tag "stable%player's uuid%":
                    kill loop-entity

        set {_loc} to {stableLoc::%player's uuid%}
        add 2 to y-coord of {_loc}

        spawn armor stand at {_loc}
        set gravity of last spawned armor stand to false
        last spawned armor stand.setMarker(true)
        last spawned armor stand.setVisible(false)
        last spawned armor stand.setCustomName("&7%player%&7&l의 마구간")
        last spawned armor stand.setCustomNameVisible(true)
        set scoreboard tags of last spawned armor stand to "stable%player's uuid%"

        

every 1 seconds:
    loop all players:
        if {stableLoc::%loop-player's uuid%} is set:
            loop all horses:
                if scoreboard tags of loop-horse is "%loop-player's uuid%":
                    if distance between {stableLoc::%loop-player's uuid%} and loop-horse > 10:
                        if distance between loop-horse and loop-player > 50:
                            set {_groundLoc} to {stableLoc::%loop-player's uuid%}
                            add 1 to y-coord of {_groundLoc}
                            teleport loop-horse to {_groundLoc}



command /hfGive:
    trigger:
        if player is op:
            give player {flute}
            give player {stable}
        else:
            message "&cOP required"
